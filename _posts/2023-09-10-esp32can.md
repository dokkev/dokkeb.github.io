---
title: "[CAN 03] - Communicating to ESP32 with SocketCAN" 
description: SocketCAN Communication with ESP32
layout: distill
published: true
date: 2023-09-10 00:00:00
img: /assets/img/can/socketcan.jpg
permalink: /esp32can/
tags: [Hardware Development]
# categories: robotics101

---

Previous Posts:
- [What is CAN?](/aboutcan/)
- [Setting up SocketCAN on Linux](/socketcan/)


## Hardware

I will be using USB-CAN adapter to connect my Ubuntu computer to ESP32 with TJA1050 CAN transceiver. Since [ESP32 already includes CAN controller](https://docs.espressif.com/projects/esp-idf/en/release-v3.3/api-reference/peripherals/can.html), we only need CAN transceiver. Note that not every microcontroller comes with CAN controller, so you may need to use external CAN controller like MCP2515.

<div class="row">
    <div class="col-sm mt-3 mt-md-0">
        {% include figure.liquid loading="eager" path="assets/img/can/esp32.jpg" title="esp32" class="img-fluid rounded z-depth-1" %}
    </div>
    <div class="col-sm mt-3 mt-md-0">
        {% include figure.liquid loading="eager" path="assets/img/can/tja.jpg" title="tja1050" class="img-fluid rounded z-depth-1" %}
    </div>
        <div class="col-sm mt-3 mt-md-0">
        {% include figure.liquid loading="eager" path="assets/img/can/usb-can.jpg" title="usb-can" class="img-fluid rounded z-depth-1" %}
    </div>
</div>
<div class="caption">
  Left: ESP32 Middle: TJA1050 Right: USB-CAN Adapter
</div>


<div class="row">
    <div class="col-sm mt-3 mt-md-0">
        {% include figure.liquid loading="eager" path="assets/img/can/wiring.jpg" title="wiring" class="img-fluid rounded z-depth-1" %}
    </div>
</div>
<div class="caption">
  Wiring ESP32 to TJA1050 and USB-CAN Adapter
</div>

## Setting up your ESP32 with CAN Transceiver

Supply 5V to the TJA1050 and connect the CANH and CANL to corresponding GPIO pins on ESP32. Any GPIO pins can be used as long as they have read and write capabilities.
The default GPIO pins for CAN TX and RX on ESP32 are GPIO 16 (RX) and GPIO 17 (TX). 

I will be using Arduino IDE to program ESP32 with [Arduino CAN library](https://github.com/sandeepmistry/arduino-CAN) which provides good high-level API for CAN communication.

You can add ESP32 boards to Arduino IDE by following the instructions [here](https://randomnerdtutorials.com/installing-the-esp32-board-in-arduino-ide-windows-instructions/).


>IMPORTANT! Note that there is a [glitch that ESP32 cuts the baudrate in half](https://github.com/sandeepmistry/arduino-CAN/issues/62). If you want to set the baudrate to 500 kbps, you need to set it to 1000 kbps (1 Mbps) in the code like this: `CAN.begin(1000E3);`

If you want to resolve it add the following line to `ESP32SJA1000.cpp` in `CAN/src` in Arduino library at line `126`
```
modifyRegister(REG_BTR1, 0x80, 0x80); // SAM = 1
writeRegister(REG_IER, 0xff); // enable all interrupts
modifyRegister(REG_IER, 0x10, 0); //brp_div will prescale BRP by 2. Only available on ESP32
Revision 2 or later. Reserved otherwise 
```


<details>
  <summary>CAN Sender/Receiver Code for ESP32:</summary>


```c++
//==================================================================================//
#include <CAN.h>
#define TX_GPIO_NUM 17 // Connects to CTX
#define RX_GPIO_NUM 16 // Connects to CRX
//==================================================================================//
void setup() {
 Serial.begin (115200);
 while (!Serial);
 delay (1000);
 Serial.println ("CAN Receiver/Receiver");
 // Set the pins
 CAN.setPins (RX_GPIO_NUM, TX_GPIO_NUM);
 // start the CAN bus at 500 kbps (Baudrate is cut in half)
 if (!CAN.begin (1000E3)) {
 Serial.println ("Starting CAN failed!");
 while (1);
 }
 else {
 Serial.println ("CAN Initialized");
 }
}
//==================================================================================//
void loop() {
 canSender();
 canReceiver();
}
//==================================================================================//
void canSender() {
 // send packet: id is 11 bits, packet can contain up to 8 bytes of data
 Serial.print ("Sending packet ... ");
 CAN.beginPacket (0x12); //sets the ID and clears the transmit buffer
 // CAN.beginExtendedPacket(0xabcdef);
 CAN.write ('h'); //write data to buffer. data is not sent until endPacket() is called.
 CAN.write ('e');
 CAN.write ('l');
 CAN.write ('l');
 CAN.write ('o');
 CAN.endPacket();
 Serial.print ("packet end ... ");
 //RTR packet with a requested data length
 CAN.beginPacket (0x12, 3, true);
 CAN.endPacket();
 Serial.println ("done");
 delay (1000);
}
//==================================================================================//
void canReceiver() {
 // try to parse packet
 int packetSize = CAN.parsePacket();
 if (packetSize) {
 // received a packet
 Serial.print ("Received ");
 if (CAN.packetExtended()) {
 Serial.print ("extended ");
 }
 if (CAN.packetRtr()) {
 // Remote transmission request, packet contains no data
 Serial.print ("RTR ");
 }
 Serial.print ("packet with id 0x");
 Serial.print (CAN.packetId(), HEX);
 if (CAN.packetRtr()) {
 Serial.print (" and requested length ");
 Serial.println (CAN.packetDlc());
 } else {
 Serial.print (" and length ");
 Serial.println (packetSize);
 // only print packet data for non-RTR packets
 while (CAN.available()) {
 Serial.print ((char) CAN.read());
 }
 Serial.println();
 }
 Serial.println();
 }
}
//==================================================================================//
```
</details>

Flash the code above to ESP32. If you get error message: `A fatal error occurred: Could not open /dev/ttyUSB0, the port doesn't exist Failed uploading: uploading error: exit status 2`, run the following command:
```
sudo adduser <username> dialout
sudo chmod a+rw /dev/ttyUSB0
```
Change `<username>` to your username and `/dev/ttyUSB0` to your USB port.


This code reads CAN messages and print them to the serial monitor. It also sends a CAN message with the data `hello` and RTR request every second.

If the CAN transceiver is connected correctly, ESP32 will fail to send the CAN message and get stuck in `CAN.endPacket()` function.


## Reading CAN from Terminal

Set the CAN state to `UP` with a desired bitrate. Assuming you are using `candlelight` firmware, you can set the bitrate to 500 kbps by running:
```
sudo ip link set up can0 type can bitrate 500000
```

Open a new terminal and run:
```
candump can0
```

<div class="row">
    <div class="col-sm mt-3 mt-md-0">
        {% include figure.liquid loading="eager" path="assets/img/can/read_hello.jpg" title="read_hello" class="img-fluid rounded z-depth-1" %}
    </div>
</div>
<div class="caption">
  Received CAN messages from ESP32 by `candump can0`
</div>

The CAN message data is in the hexadecimal format. You can convert it using [ASCII table](https://www.rapidtables.com/convert/number/hex-to-ascii.html)

`68 65 6C 6C 6F => hello`

## Writing CAN from Terminal

Now let's send CAN messages to ESP32. Using th ASCII table, we can find the hexadecimal value of `hello` is `68 65 6C 6C 6F`.
On the terminal, we can send the CAN message with desired ID and data with the following format: `cansend [options] <interface> <CAN ID>#<data>`

Run the following command on a terminal to send the "hello" CAN message with CAN ID 0x123 to ESP32:

If you didn't connect your CAN transceiver + ESP32 to the USB-CAN adapter correctly, the TX buffer will be full and the CAN message will not be sent.

Upon sucessful connection, you will see the CAN messages received in the serial monitor of Arduino IDE.

```
cansend can0 123#68656C6C6F
```
<div class="row">
    <div class="col-sm mt-3 mt-md-0">
        {% include figure.liquid loading="eager" path="assets/img/can/hello_esp.jpg" title="hello_esp" class="img-fluid rounded z-depth-1" %}
    </div>
</div>
<div class="caption">
  Received CAN messages displayed in the serial monitor of Arduino IDE
</div>


Now let's try to send random CAN messages to ESP32

Run the following command:
```
cangen can0
```
This command will start generating random CAN messages continuously. 


<div class="row">
    <div class="col-sm mt-3 mt-md-0">
        {% include figure.liquid loading="eager" path="assets/img/can/cangen_esp.jpg" title="cangen_esp" class="img-fluid rounded z-depth-1" %}
    </div>
</div>
<div class="caption">
  Received CAN messages displayed in the serial monitor of Arduino IDE
</div>

Messages are not encoded correctly because canReceiver() function converts the data to char using ASCII. You can modify the function to print the data in hexadecimal format.
Instead of `Serial.print ((char) CAN.read());`, you can use `Serial.print (CAN.read(), HEX);` to print the data in hexadecimal format.



